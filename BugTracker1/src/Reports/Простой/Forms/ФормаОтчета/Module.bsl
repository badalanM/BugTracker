&НаСервере
Процедура РезультатОбработкаРасшифровкиНаСервере(Расшифровка,Сайт)

	Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	ЭлементыРасшифровки = Данные.Элементы.Получить(Расшифровка);
	Поля = ЭлементыРасшифровки.ПолучитьПоля(); 
	
	Для Каждого ПоляРасшифровки Из Поля Цикл
		Если ПоляРасшифровки.Поле = "СсылкаJIRA" И ЗначениеЗаполнено(ПоляРасшифровки.Значение) Тогда 
			Сайт = ПоляРасшифровки.Значение;
			Прервать;
		КонецЕсли;	
	КонецЦикла;		
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	Сайт = "";
	Если ТипЗнч(Расшифровка) = Тип("Строка") И Найти(НРег(Расшифровка),"http") > 0 Тогда
		Сайт = Расшифровка;	
	ИначеЕсли ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
		РезультатОбработкаРасшифровкиНаСервере(Расшифровка,Сайт);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Сайт) Тогда	
		ЗапуститьПриложение(Сайт);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыСКД(ИмяПараметра, Значение)
	
	ПараметрСКД = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрСКД <> Неопределено Тогда
		ПараметрСКД.Использование = Истина;
		ПараметрСКД.Значение  = Значение;
	КонецЕсли;
	
	ПользовательскийПараметрСКД = Новый ПараметрКомпоновкиДанных(ИмяПараметра);
	
	Для Каждого ПользовательскаяНастройка Из Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ПользовательскаяНастройка) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")
			И ПользовательскаяНастройка.Параметр = ПользовательскийПараметрСКД Тогда
			ПользовательскаяНастройка.Значение = Значение;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НачалоТекущейНедели    = НачалоНедели(ТекущаяДата());
	НачалоПредыдущейНедели = НачалоНедели(НачалоТекущейНедели-1);

	ПериодОтчета = Новый СтандартныйПериод;
	ПериодОтчета.ДатаНачала    = НачалоПредыдущейНедели;
	ПериодОтчета.ДатаОкончания = КонецДня(ТекущаяДата());
	
	УстановитьПараметрыСКД("ПериодОтчета", ПериодОтчета);
	
КонецПроцедуры